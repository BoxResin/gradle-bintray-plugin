buildscript {
  ext {
    kotlinVersion = "1.3.50"
  }

  repositories {
    jcenter()
    google()
    maven { url 'https://dl.bintray.com/minyushov/gradle' }
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath "com.minyushov.gradle:bintray:1.2.4"
  }
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'com.minyushov.bintray'

repositories {
  jcenter()
  google()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

  implementation "com.android.tools.build:gradle:3.6.0-alpha09"

  implementation "org.jetbrains.dokka:dokka-gradle-plugin:0.9.18"
  implementation "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"

  implementation("com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4") {
    exclude group: "xerces", module: "xercesImpl"
  }

  testImplementation "junit:junit:4.12"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

group = 'com.minyushov.gradle'
version = '2.0-dev'

gradlePlugin {
  plugins {
    customPlugin {
      id = 'com.minyushov.bintray'
      implementationClass = 'com.minyushov.bintray.BintrayPlugin'
    }
  }
}

bintrayUpload {
  dryRun = false
  repo = "gradle"
  groupId = "com.minyushov.gradle"
  artifactId = "bintray"
  version = "1.2.4"
  vcsUrl = "https://github.com/minyushov/gradle-bintray-plugin.git"
  license = "Apache-2.0"
  docs = false
}